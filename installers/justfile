# https://github.com/casey/just


# Run ansible on the local workstation.
ansible *args:
    ansible-playbook -v -c local -i localhost, install.yml {{args}}
 

# Remove an executable, if found.
rmbin cmd:
    sudo rm `which {{cmd}}`


# Install non-packaged software.
install: _amber _jiq _up _fonts-hack _vim-vundle

_amber:
    fetchbin.sh ambs https://github.com/dalance/amber/releases/download/v0.5.9/amber-v0.5.9-x86_64-lnx.zip
    fetchbin.sh ambr https://github.com/dalance/amber/releases/download/v0.5.9/amber-v0.5.9-x86_64-lnx.zip

_jiq:
    fetchbin.sh jiq https://github.com/fiatjaf/jiq/releases/download/0.7.1/jiq_linux_amd64
_up:
    fetchbin.sh up https://github.com/akavel/up/releases/download/v0.4/up

_sysz:
    # https://github.com/joehillen/sysz
    wget -O ~/.local/bin/sysz https://raw.githubusercontent.com/joehillen/sysz/master/sysz
    chmod +x ~/.local/bin/sysz


# Install font Hack from https://github.com/ryanoasis/nerd-fonts
_fonts-hack version="2.1.0":
    #!/usr/bin/env bash
    set -euxo pipefail
    test -d ~/.local/share/fonts/Hack && exit
    cd /tmp
    wget -nv -c "https://github.com/ryanoasis/nerd-fonts/releases/download/v{{version}}/Hack.zip"
    test -d ~/.fonts/Hack && rm -r ~/.fonts/Hack || true
    mkdir -p ~/.local/share/fonts/Hack
    unzip -d ~/.local/share/fonts/Hack Hack.zip
    fc-cache -fv


# Clone Vim Vundle to your ~/.vim/.
_vim-vundle:
    #!/usr/bin/env bash
    set -euxo pipefail
    test -d ~/.vim/bundle/Vundle.vim && exit
    git clone https://github.com/VundleVim/Vundle.vim.git ~/.vim/bundle/Vundle.vim


asdf:
    -asdf plugin-add awscli
    -asdf plugin-add aws-sam-cli
    -asdf plugin-add eksctl
    -asdf plugin-add gcloud
    -asdf plugin-add github-cli
    -asdf plugin-add hugo
    -asdf plugin-add k3d
    -asdf plugin-add k9s
    -asdf plugin-add kubectl
    -asdf plugin-add packer
    -asdf plugin-add poetry
    -asdf plugin-add pre-commit
    -asdf plugin-add task
    -asdf plugin-add terraform

    -asdf install awscli
    #-asdf install aws-sam-cli
    -asdf install eksctl
    -asdf install gcloud
    -asdf install github-cli
    -asdf install hugo
    -asdf install k3d
    -asdf install k9s
    -asdf install kubectl
    -asdf install packer
    -asdf install poetry
    -asdf install pre-commit
    -asdf install task
    -asdf install terraform




# Fedora update.
update:
    sudo dnf update --downloadonly -y
    sudo dnf update -y
    flatpak update -y

# Fedora upgrade, step 0.
upgrade0:
    sudo dnf install dnf-plugin-system-upgrade
    sudo dnf upgrade --refresh
    @echo Reboot your computer

# Fedora upgrade, step 1.
upgrade1 version="`expr $(lsb_release -r | cut -f2) + 1`":
    # https://docs.fedoraproject.org/en-US/quick-docs/dnf-system-upgrade/
    sudo dnf system-upgrade download -y --releasever={{version}}

# Fedora upgrade, step 2.
upgrade2:
    sudo dnf system-upgrade reboot


# Reclaim unused space.
reclaim:
    sudo dnf clean packages
    sudo dnf system-upgrade clean
    flatpak uninstall --unused
    flatpak uninstall --delete-data
    sudo journalctl --vacuum-time=10d


# Remove old kernels.
kernel-cleanup:
    #!/usr/bin/env bash
    set -euo pipefail
    old_kernels=($(dnf repoquery --installonly --latest-limit=-1 -q | grep -v `uname -r` || true))
    if ! sudo dnf remove "${old_kernels[@]}"; then
      echo "Failed to remove old kernels"
      exit 1
    fi


# Tidy-up system-wide.
fight-entropy:
    @echo Modules from older releases
    -ls -1d /lib/modules/* | grep -v ".fc`lsb_release -sr`."
    @echo

    @echo Packages with broken dependencies
    sudo dnf repoquery --unsatisfied
    @echo

    @echo Packages with multiple versions installed
    sudo dnf repoquery --duplicates
    @echo

    @echo Possibly unused packages
    sudo dnf repoquery --extras --exclude=kernel,kernel-\*,kmod-\*,dkms | grep -v ".fc`lsb_release -sr`." 
    @echo

    #@echo Broken symlinks
    #sudo symlinks -r /usr | grep dangling
    #@echo


# Delete everything installed by pipx.
pipx-cleanup:
    rm -r ~/.local/pipx
    symlinks -d ~/.local/bin | grep -v absolute



################################################## 
#

# Post-install config tasks.
tweak: _tweak-vbox _tweak-firefox _tweak-kde

_tweak-vbox:
    # Set folder to store Virtualbox machines
    VBoxManage setproperty machinefolder /home/jordif/spool/VirtualBox_VMs

# Minor Firefox tweaks.
_tweak-firefox:
    test -f ~/.gtkrc-2.0 || touch ~/.gtkrc-2.0
    sed -i 's/# *gtk-key-theme-name =/gtk-key-theme-name =/' ~/.gtkrc-2.0
    grep -q 'gtk-key-theme-name =' ~/.gtkrc-2.0 \
    || echo 'gtk-key-theme-name = "Emacs"' >> ~/.gtkrc-2.0
    # Firefox uses kde file manager: requires xdg-desktop-portal-kde
    # Set the app launcher command to: GTK_USE_PORTAL=1 firefox %u

# KDE tweaks.
_tweak-kde:
    # https://userbase.kde.org/Plasma/Tips
    # 'Meta' opens krunner
    # kwriteconfig5 --file ~/.config/kwinrc --group ModifierOnlyShortcuts --key Meta "org.kde.kglobalaccel,/component/krunner,org.kde.kglobalaccel.Component,invokeShortcut,run command"
    # qdbus org.kde.KWin /KWin reconfigure
    #
    # 'Meta' does nothing by itself
    kwriteconfig5 --file ~/.config/kwinrc --group ModifierOnlyShortcuts --key Meta ""
    qdbus org.kde.KWin /KWin reconfigure

# configure xkb system-wide
config-x11:
    sudo install -m 0644 files/usr/share/X11/xkb/rules/base.lst /usr/share/X11/xkb/rules/
    sudo install -m 0644 files/usr/share/X11/xkb/symbols/us_funollet /usr/share/X11/xkb/symbols/
    # sudo install -m 0644 files/usr/share/X11/xkb/symbols/notanumpad /usr/share/X11/xkb/symbols/
    # sudo install -m 0644 files/usr/share/X11/xkb/symbols/us_numbers_level2 /usr/share/X11/xkb/symbols/
    sudo localectl set-x11-keymap us_funollet pc105 "" \
        "compose:caps,shift:both_capslock,ctrl:menu_rctrl,terminate:ctrl_alt_bksp"

# reconfigure xkb on a running X session
xkb-change:
    # Delete previous options.
    setxkbmap -layout us -option ''
    # Set keyboard layout.
    # compose:caps          caps key is compose key
    # shift:both_capslock   Both Shift together enable Caps Lock
    setxkbmap -layout us_funollet \
        -option 'compose:caps' \
        -option 'shift:both_capslock' \
        -option 'terminate:ctrl_alt_bksp' \
        -option 'ctrl:menu_rctrl'
    # -option 'caps:none' \
    # -option 'compose:ralt' \
    # -option 'grp:caps_toggle' \
    #       switch layouts with caps key
