# https://github.com/casey/just


# Run ansible on the local workstation.
ansible *args:
    ansible-playbook -v -c local -i localhost, install.yml {{args}}
 

# install with uv
uv-install:
  uv tool install --python 3.13 pex
  uv tool install --python 3.13 csb43
  uv tool install --python 3.13 termdown
  uv tool install --python 3.13 yamlfix
  uv tool install --python 3.12 posting
  uv tool install --python 3.13 copier
  uv tool install --python 3.13 vint

# Install using bin
_bin-install repo:
  which {{file_name(repo)}} > /dev/null || bin install {{repo}}

# Install non-packaged software.
install: _amber _lazycomplete _fonts-hack _keymapp _kubefwd _up (_wally-cli "2.0.0-linux")

_amber version=`http https://api.github.com/repos/dalance/amber/releases | jq -r .[].tag_name | head -1`:
    #!/usr/bin/env bash
    set -euxo pipefail
    which ambr > /dev/null && exit
    wget -q -c -P /tmp https://github.com/dalance/amber/releases/download/{{version}}/amber-{{version}}-x86_64-lnx.zip
    unzip /tmp/amber-{{version}}-x86_64-lnx.zip ambr ambs -d ~/.local/bin

_lazycomplete:
    which lazycomplete > /dev/null || sudo dnf install -y https://github.com/rsteube/lazycomplete/releases/download/v0.0.19/lazycomplete_0.0.19_linux_amd64.rpm

_kubefwd:
  sudo dnf install -y https://github.com/txn2/kubefwd/releases/download/1.22.5/kubefwd_amd64.rpm

_keymapp:
  #!/usr/bin/env bash
  set -euxo pipefail
  name=keymapp
  latest="https://oryx.nyc3.cdn.digitaloceanspaces.com/keymapp/keymapp-latest.tar.gz"
  [[ -f ~/.local/bin/$name ]] && exit
  mkdir /tmp/$name && cd /tmp/$name
  wget -nv -c $latest
  tar -xzf $name*.tar.gz
  install $name ~/.local/bin

_up:
  which up > /dev/null || bin install https://github.com/akavel/up

_wally-cli version=`http https://api.github.com/repos/zsa/wally-cli/releases | jq -r .[].tag_name | grep -i linux | head -1`:
    which wally-cli > /dev/null || bin install https://github.com/zsa/wally-cli/releases/tag/{{version}}

# Install font Hack from https://github.com/ryanoasis/nerd-fonts
_fonts-hack version="2.1.0":
    #!/usr/bin/env bash
    set -euxo pipefail
    test -d ~/.local/share/fonts/Hack && exit
    cd /tmp
    wget -nv -c "https://github.com/ryanoasis/nerd-fonts/releases/download/v{{version}}/Hack.zip"
    test -d ~/.fonts/Hack && rm -r ~/.fonts/Hack || true
    mkdir -p ~/.local/share/fonts/Hack
    unzip -d ~/.local/share/fonts/Hack Hack.zip
    fc-cache -fv




# Fedora update.
update:
    sudo dnf update --downloadonly -y
    sudo dnf update -y
    flatpak update -y

# Fedora upgrade, step 0.
upgrade0:
    sudo dnf install dnf-plugin-system-upgrade
    sudo dnf upgrade --refresh
    @echo Reboot your computer

# Fedora upgrade, step 1.
upgrade1 version="`expr $(lsb_release -r | cut -f2) + 1`":
    # https://docs.fedoraproject.org/en-US/quick-docs/dnf-system-upgrade/
    sudo dnf system-upgrade download -y --releasever={{version}}

# Fedora upgrade, step 2.
upgrade2:
    sudo dnf system-upgrade reboot


# Reclaim unused space.
reclaim:
    sudo dnf clean packages
    sudo dnf system-upgrade clean
    flatpak uninstall --unused
    flatpak uninstall --delete-data
    sudo journalctl --vacuum-time=10d


# Remove old kernels.
kernel-cleanup:
    #!/usr/bin/env bash
    set -euo pipefail
    old_kernels=($(dnf repoquery --installonly --latest-limit=-1 -q | grep -v `uname -r` || true))
    if ! sudo dnf remove "${old_kernels[@]}"; then
      echo "Failed to remove old kernels"
      exit 1
    fi


# Tidy-up system-wide.
fight-entropy:
    @echo Modules from older releases
    -ls -1d /lib/modules/* | grep -v ".fc`lsb_release -sr`."
    @echo

    @echo Packages with broken dependencies
    sudo dnf repoquery --unsatisfied
    @echo

    @echo Packages with multiple versions installed
    sudo dnf repoquery --duplicates
    @echo

    @echo Possibly unused packages
    sudo dnf repoquery --extras --exclude=kernel,kernel-\*,kmod-\*,dkms | grep -v ".fc`lsb_release -sr`." 
    @echo

    #@echo Broken symlinks
    #sudo symlinks -r /usr | grep dangling
    #@echo


################################################## 
#

# Post-install config tasks.
tweak: _tweak-vbox _tweak-firefox _tweak-kde

_tweak-vbox:
    # Set folder to store Virtualbox machines
    VBoxManage setproperty machinefolder /home/jordif/spool/VirtualBox_VMs

# Minor Firefox tweaks.
_tweak-firefox:
    test -f ~/.gtkrc-2.0 || touch ~/.gtkrc-2.0
    sed -i 's/# *gtk-key-theme-name =/gtk-key-theme-name =/' ~/.gtkrc-2.0
    grep -q 'gtk-key-theme-name =' ~/.gtkrc-2.0 \
    || echo 'gtk-key-theme-name = "Emacs"' >> ~/.gtkrc-2.0
    # Firefox uses kde file manager: requires xdg-desktop-portal-kde
    # Set the app launcher command to: GTK_USE_PORTAL=1 firefox %u

# KDE tweaks.
_tweak-kde:
    # https://userbase.kde.org/Plasma/Tips
    # 'Meta' opens krunner
    # kwriteconfig5 --file ~/.config/kwinrc --group ModifierOnlyShortcuts --key Meta "org.kde.kglobalaccel,/component/krunner,org.kde.kglobalaccel.Component,invokeShortcut,run command"
    # qdbus org.kde.KWin /KWin reconfigure
    #
    # 'Meta' does nothing by itself
    kwriteconfig5 --file ~/.config/kwinrc --group ModifierOnlyShortcuts --key Meta ""
    qdbus org.kde.KWin /KWin reconfigure

# configure xkb system-wide
config-x11:
    # sudo install -m 0644 files/usr/share/X11/xkb/rules/base.lst /usr/share/X11/xkb/rules/
    # sudo install -m 0644 files/usr/share/X11/xkb/symbols/us_funollet /usr/share/X11/xkb/symbols/
    # sudo install -m 0644 files/usr/share/X11/xkb/symbols/notanumpad /usr/share/X11/xkb/symbols/
    # sudo install -m 0644 files/usr/share/X11/xkb/symbols/us_numbers_level2 /usr/share/X11/xkb/symbols/
    sudo localectl set-x11-keymap us pc105 "" \
        "compose:caps,ctrl:menu_rctrl,terminate:ctrl_alt_bksp"

# reconfigure xkb on a running X session
xkb-change:
    # Delete previous options.
    setxkbmap -layout us -option ''
    # Set keyboard layout.
    # compose:caps          caps key is compose key
    # shift:both_capslock   Both Shift together enable Caps Lock
    setxkbmap -layout us \
        -option 'compose:caps' \
        -option 'terminate:ctrl_alt_bksp' \
        -option 'ctrl:menu_rctrl'
    # -option 'caps:none' \
    # -option 'compose:ralt' \
    # -option 'grp:caps_toggle' \
    #       switch layouts with caps key
