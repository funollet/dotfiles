#!/usr/bin/env -S just --justfile
# me
#
# https://github.com/casey/just

# show this message
help:
  @just -l --unsorted --justfile {{justfile()}}


# Sync files with ssh tunnel + tmux.
sync quick="n":
    tmux has -t tunnel || tmux new -s tunnel \; detach
    tmux at -t tunnel \; new-window ssh tunnel \; detach
    sleep 2
    unison -ui text \
        {{ if quick == "n" {""} else {"-ignore 'Path fotos'"} }} \
        /home/jordif \
        ssh://localhost:2222


# (Local network) Sync files with ssh tunnel + tmux.
sync-local quick="n":
    #!/usr/bin/env bash
    set -euxo pipefail
    
    remote="ssh://localhost:2222"
    if ip route | grep default | grep -q 192.168.1. ; then
        if [ `hostname` = "alai.lan" ] ; then
            remote="ssh://yak.lan"
        else
            remote="ssh://alai.lan"
        fi
    fi

    unison -ui text \
        {{ if quick == "n" {""} else {"-ignore 'Path fotos' -ignore 'Path mmedia'"} }} \
        /home/jordif \
        $remote


# Pick a local cookiecutter.
cookie:
    ls -1 ~/code/skeletons/ | fzf | xargs -rI% cookiecutter ~/code/skeletons/%


# update ergodox firmware
ergodox-update url:
    wget -O /tmp/ergodox.hex {{url}}
    wally-cli /tmp/ergodox.hex
    # key repeat: <delay>ms <rate>Hz
    xset r rate 400 50
    # disable bell
    xset b off


# PCI reset of the wifi device
wifi-reset:
    sudo /bin/sh -c 'echo 1 > /sys/bus/pci/devices/0000\:00\:14.3/remove ; sleep 1 ; echo 1 > /sys/bus/pci/rescan'

# PCI reset of the usb hub device
hub-reset:
    sudo /bin/sh -c 'echo 1 > /sys/bus/pci/devices/0000\:00\:02.0/remove ; sleep 1 ; echo 1 > /sys/bus/pci/rescan'



# resync clock
clock-sync:
    sudo systemctl restart chronyd.service



# pick an xdg-autostart script and run it
dex $fname='':
  if [[ -z ${fname} ]] ; then \
    ls -1 ~/.config/autostart/*.desktop | fzf -1 | xargs dex-autostart ; \
  else \
    dex-autostart ~/.config/autostart/${fname}.desktop ; \
  fi


# tweaks after docking the laptop
dock:
  sudo systemctl restart chronyd.service
  autorandr -f 01

# tweaks before undocking the laptop
undock:
	autorandr 0

trackpad:
  bluetoothctl connect A8:91:3D:E5:86:65

# vim: ft=just
